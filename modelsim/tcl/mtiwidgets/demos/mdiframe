#!/bin/sh
# ----------------------------------------------------------------------
#  DEMO: mdi in mtiwidgets
# ----------------------------------------------------------------------
#\
exec itkwish "$0" ${1+"$@"}
package require Mtiwidgets

# itkwish interprets the rest...
# ----------------------------------------------------------------------
. configure -background white

iwidgets::optionmenu .cb -labeltext Arrange: -labelpos w -command {
    .mdi layout [.cb get]
}
pack .cb -side top

.cb insert end cascade horizontal vertical

iwidgets::optionmenu .cbtabs -labeltext "Show tabs:" -labelpos w -command {
    .mdi configure -showtabs [.cbtabs get]
}
pack .cbtabs -side top

.cbtabs insert end yes no

mtiwidgets::mdiframe .mdi -borderwidth 2 -relief sunken -mode classic
pack .mdi -side top -fill both -expand 1

.mdi client 0 -title "Example Code"
set cs [.mdi childsite 0]
iwidgets::scrolledtext $cs.st -hscrollmode dynamic -vscrollmode dynamic -wrap none
pack $cs.st -side top -expand 1 -fill both
set f1 [open [file join ${::mtiwidgets::library} demos mdiframe]]
$cs.st insert insert [read $f1]
close $f1

.mdi client 1 -title "MDI Source Code"
set cs [.mdi childsite 1]
iwidgets::scrolledtext $cs.st -hscrollmode static -vscrollmode static -wrap none
pack $cs.st -side top -expand 1 -fill both
set tcs [winfo parent [$cs.st component clipper]]
set corner [.mdi makeResizeCorner $tcs.corner 1]
grid $corner -row 2 -column 2 -sticky se
set f1 [open [file join ${::mtiwidgets::library} scripts mdi.itk]]
$cs.st insert insert [read $f1]
close $f1

.mdi client 2 -title "Column Button"
set cs [.mdi childsite 2]
frame $cs.cf -borderwidth 2 -relief sunken
pack $cs.cf 
set cf $cs.cf
mtiwidgets::colbutton $cf.cb -text "States" -command sortit
iwidgets::scrolledlistbox $cf.lb \
	-relief sunken -borderwidth 1 \
	-vscrollmode dynamic -hscrollmode none 
pack $cf.cb -fill x
pack $cf.lb

set stateList [list Maine "New Hampshire" Vermont Massachusetts "Rhode Island" Conneticut "New York" Pennsylvania "New Jersey" Delware Maryland Virgina "West Virgina" "North Carolina" Kentucky "South Carolina" Tennisee Georgia Florida]

proc sortit {} {
	global cf stateList
	switch [$cf.cb cget -sortstate] {
		up {
			$cf.cb configure -sortstate none
			set sortedList $stateList
		}
		down {
			$cf.cb configure -sortstate up
			set sortedList [lsort -dictionary -decreasing $stateList]			
		}
		default {
			$cf.cb configure -sortstate down
			set sortedList [lsort -dictionary -increasing $stateList]
		}
	}
	$cf.lb delete 0 end
	eval $cf.lb insert 0 $sortedList
}

sortit
